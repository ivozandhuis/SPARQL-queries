#+ summary: Link De Vries data from the ATM/clariahPilot project to the images of the city archives
#+ endpoint: https://api.druid.datalegend.net/datasets/amsterdamtimemachine/clariahPilot/services/clariahPilot/sparql

PREFIX foaf: <http://xmlns.com/foaf/0.1/>
PREFIX sdmx: <http://purl.org/linked-data/sdmx/2009/dimension#>
PREFIX juso: <http://rdfs.co/juso/>
PREFIX geo: <http://www.opengis.net/ont/geosparql#>
PREFIX void: <http://rdfs.org/ns/void#>
PREFIX dc: <http://purl.org/dc/elements/1.1/>
PREFIX dct: <http://purl.org/dc/terms/>
PREFIX sem: <http://semanticweb.cs.vu.nl/2009/11/sem/>

SELECT * WHERE {
  ?obs sdmx:refPeriod "1854";
       juso:address ?address .
  FILTER(regex(str(?address), "hisgis.nl", "i" )) .
  ?address geo:hasGeometry/geo:asWKT ?addressWKT .
  SERVICE <https://api.druid.datalegend.net/datasets/adamnet/all/services/endpoint/sparql>
  { ?cho void:inDataset <https://data.adamlink.nl/saa/beeldbank/> ;
         dc:title ?title ;
         dct:spatial ?address ;
         sem:hasEndTimeStamp ?date ;
         foaf:depiction ?img .
         BIND (year(xsd:dateTime(?date)) AS ?year) .
         FILTER (?year < 1875) .
         FILTER (?year > 1835) .
  }
} ORDER BY ?year 